/************************************************************************************************************/
/* Copyright 2016 DigiPen, All Rights Reserved                                                              */
/* Last Update: 1/3/2016                                                                                    */
/* Author: Zero Engine Team                                                                                 */
/* Last updated by: Arend Danielek                                                                          */
/* Class: Seek                                                                                              */
/* Brief: Will cause the object to "seek" the target object by moving towards the target object's           */
/* current position                                                                                         */
/************************************************************************************************************/

class Seek : ZilchComponent
{
    [Dependency]
    var SteeringAccumulator : SteeringAccumulator = null;
    
    // This component requires a rigidBody component
    [Dependency]
    var RigidBody : RigidBody = null;
    
    // Seek requires an Orientation & OrientationHelper component
    [Dependency]
    var Orientation : Orientation = null;
    
    [Dependency]
    var OrientationHelper : OrientationHelper = null;
    
    // Determines if the component is active or not
    [Property]
    var Active : Boolean = true;
    
    // Target object to be seek towards
    [Property]
    var Target : CogPath = null;
    
    // Range from the object in which it is valid to seek the target object
    // If the range is 0 the target object will always be seeked
    [Property]
    var SeekRange : Real
    {
        get { return this.SeekRangeDistance; }
        set
        {
            if (Math.ApproximatelyEqual(value, 0.0, 0.001))
                this.NoSeekRange = true;
            
            this.SeekRangeDistance = value;
        }
    }
    
    // Distance from the goal we're trying to seek to
    [Property]
    var GoalDistance : Real = 0.0;
    
    // Magnitude of the linear impulse to be applied each frame for seeking movement
    [Property]
    var Speed : Real = 10.0;
    
    // the rate at which the object will rotate to matchit velocity direction in degrees per second
    [Property]
    var RotationSpeed : Real = 360.0;
    
    [Property]
    var DebugDrawEnabled : Boolean = false;
    
    var SeekRangeDistance : Real = 0.0;
    var NoSeekRange : Boolean = true;
    
    // Used to track if the object is seeking
    var Seeking : Boolean = false;
    
    function Initialize(init : CogInitializer)
    {
        Zero.Connect(this.SteeringAccumulator.Owner, Events.LogicUpdate, this.OnLogicUpdate);
    }
    
    function OnLogicUpdate(event : UpdateEvent)
    {
        // If this component is not active do nothing
        if (!this.Active || !this.SteeringAccumulator.Active)
        {
            return;
        }
        
        // Calculate a vector to the target object
        var targDir = this.Target.Cog.Transform.Translation - this.Owner.Transform.Translation;
        var targDis = Math.Length(targDir);
        
        // Normalize the vector to the target object so it does not affect movement speed
        targDir /= targDis;
        
        // Check if the target object is with in the range to seek it
        if ((this.NoSeekRange || targDis < this.SeekRange) && targDis > this.GoalDistance)
        {
            // Record that the object is seeking the target object
            this.Seeking = true;
            
            // Apply a linear impulse to move the object towards the target object's position
            this.RigidBody.ApplyLinearImpulse(targDir * this.Speed);
        }
        else
        {
            // If the target object is out of seek range record that the targ object is no longer being seeked out
            this.Seeking = false;
            
            // Bring the object to a smooth stop, based on it's linear distance from the target
            var ratio = targDis / this.GoalDistance;
            
            this.RigidBody.ApplyLinearImpulse(targDir * this.Speed * ratio);
        }
        
        // Align the object at the given rotation rate to the direction of its velocity
        var newRot = this.Orientation.GetLookAtDirectionRotation(Math.Normalize(this.Owner.RigidBody.Velocity));
        
        this.Owner.Transform.Rotation = Math.Slerp(
            this.Owner.Transform.Rotation, newRot, 
            Math.ToRadians(this.RotationSpeed) * event.Dt
        );
        
        if (this.DebugDrawEnabled)
        {
            this.DrawDebug(event.Dt);
        }
    }
    
    function DrawDebug(dt : Real)
    {
        var line = new DebugLine();
        line.Start = this.Owner.Transform.Translation;
        line.End = this.Owner.Transform.Translation + this.Owner.RigidBody.Velocity;
        line.HeadSize = 0.5; 
        line.Color = Real4(0.0,1.0,0.0,1.0);
        DebugDraw.Add(line);
    }
}